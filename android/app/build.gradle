def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.protobuf'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {

    ndkVersion "22.1.7171670"

    bundle {
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }

    flavorDimensions "versionCode"

    useLibrary 'org.apache.http.legacy'

//    useLibrary 'android.test.runner'

    dexOptions {
        javaMaxHeapSize "8g"
        preDexLibraries true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/maven/commons-io/commons-io/pom.xml'
        exclude 'META-INF/maven/commons-io/commons-io/pom.properties'
        exclude("META-INF/*.kotlin_module")
        exclude 'README.txt'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    // Use now as the user-visible version
    def now = new Date()

    // Use configured versionCode if available
    def code = getInt("versionCode")

    if (code == 0) {
        // Calculate the versionCode as the number of seconds from a fixed start
        // point so that it's always increasing.
        def start = new Date(2015, 1, 1).getTime()
        code = (int) ((now.getTime() - start) / 1000)
    }

    defaultConfig {

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['resourcePackageName': 'org.getlantern.lantern', "androidManifestFile": "$projectDir/src/main/AndroidManifest.xml".toString()]
            }
        }

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        applicationId "org.getlantern.lantern"
        versionCode code

        if (project.hasProperty('lanternVersion')) {
            versionName "$project.lanternVersion ($project.lanternRevisionDate)"
        } else {
            versionName "3.0.0"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        release {
            if (project.hasProperty("KEYSTORE_FILE")) {
                storeFile file(KEYSTORE_FILE)
                keyAlias "Lantern Release Key"
                storePassword KEYSTORE_PWD
                keyPassword KEY_PWD
            }
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            // Run code coverage reports by default on debug builds.
            buildConfigField "boolean", "DEVELOPMENT_MODE", "true"
            buildConfigField "boolean", "STICKY_CONFIG", getBoolean("stickyConfig")
            buildConfigField "boolean", "STAGING", getBoolean("useStaging")
            buildConfigField "boolean", "PLAY_VERSION", getBoolean("playVersion")
            buildConfigField "boolean", "YINBI_ENABLED", getBoolean("enableYinbi")
            buildConfigField "String", "COUNTRY", userCountry()
            buildConfigField "String", "PAYMENT_PROVIDER", paymentProvider()
            buildConfigField "String", "PRO_SERVER_URL", proServerUrl()
            buildConfigField "String", "LOCONF_URL", '"https://raw.githubusercontent.com/getlantern/loconf/master/test-messages.json"'
            buildConfigField "String", "PRIVACY_POLICY_URL", '"https://s3.amazonaws.com/lantern/LanternPrivacyPolicy.pdf"'
            buildConfigField "String", "TERMS_OF_SERVICE_URL", '"https://s3.amazonaws.com/lantern/Lantern-TOS.pdf"'
        }

        profile {
            minifyEnabled false
            shrinkResources false
            debuggable false
            buildConfigField "boolean", "DEVELOPMENT_MODE", "true"
            buildConfigField "boolean", "STICKY_CONFIG", getBoolean("stickyConfig")
            buildConfigField "boolean", "STAGING", getBoolean("useStaging")
            buildConfigField "boolean", "PLAY_VERSION", getBoolean("playVersion")
            buildConfigField "boolean", "YINBI_ENABLED", getBoolean("enableYinbi")
            buildConfigField "String", "COUNTRY", userCountry()
            buildConfigField "String", "PAYMENT_PROVIDER", paymentProvider()
            buildConfigField "String", "PRO_SERVER_URL", proServerUrl()
            buildConfigField "String", "LOCONF_URL", '"https://raw.githubusercontent.com/getlantern/loconf/master/test-messages.json"'
            buildConfigField "String", "PRIVACY_POLICY_URL", '"https://s3.amazonaws.com/lantern/LanternPrivacyPolicy.pdf"'
            buildConfigField "String", "TERMS_OF_SERVICE_URL", '"https://s3.amazonaws.com/lantern/Lantern-TOS.pdf"'
        }

        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            buildConfigField "boolean", "DEVELOPMENT_MODE", "false"
            buildConfigField "boolean", "STICKY_CONFIG", getBoolean("stickyConfig")
            buildConfigField "boolean", "STAGING", getBoolean("useStaging")
            buildConfigField "boolean", "PLAY_VERSION", getBoolean("playVersion")
            buildConfigField "boolean", "YINBI_ENABLED", getBoolean("enableYinbi")
            buildConfigField "String", "COUNTRY", userCountry()
            buildConfigField "String", "PAYMENT_PROVIDER", paymentProvider()
            buildConfigField "String", "PRO_SERVER_URL", proServerUrl()
            buildConfigField "String", "LOCONF_URL", '"https://raw.githubusercontent.com/getlantern/loconf/master/messages.json"'
            buildConfigField "String", "PRIVACY_POLICY_URL", '"https://s3.amazonaws.com/lantern/LanternPrivacyPolicy.pdf"'
            buildConfigField "String", "TERMS_OF_SERVICE_URL", '"https://s3.amazonaws.com/lantern/Lantern-TOS.pdf"'

            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            firebaseCrashlytics {
                // Enable processing and uploading of native symbols to Crashlytics servers.
                // By default, this is disabled to improve build speeds.
                // This flag must be enabled to see properly-symbolicated native
                // stack traces in the Crashlytics dashboard.
                nativeSymbolUploadEnabled true
                unstrippedNativeLibsDir '../../build/app/intermediates/merged_native_libs/prodRelease/out/lib/'
                strippedNativeLibsDir '../../build/app/intermediates/stripped_native_libs/prodRelease/out/lib/'
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            java.srcDirs += 'src/main/kotlin'
            proto.srcDirs += '../../protos_shared'
        }
    }

    productFlavors {
        prod {
        }
        autoTest {
            versionCode 1 // so we can always test auto-update.
        }
    }

    splits {
        abi {
            enable true
            reset()
            include androidArchJava()
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

def androidArch() {
    def value = project.getProperties().get("androidArch")
    if (value == null || !value?.trim()) {
        return "all"
    }
    return value
}

def androidArchJava() {
    def value = project.getProperties().get("androidArchJava")
    if (value == null || !value?.trim()) {
        return "armeabi-v7a arm64-v8a x86 x86_64"
    }
    return value
}

def getBoolean(name) {
    def value = project.getProperties().get(name)
    if (value == null || !value?.trim()) {
        return "false"
    }
    return value
}

def getInt(name) {
    def value = project.getProperties().get(name)
    if (value == null || !value?.trim()) {
        return 0
    }
    return value.toInteger()
}

def userCountry() {
    def value = project.getProperties().get("country")
    if (value == null || !value?.trim()) {
        return "\"\""
    }
    return String.format("\"%s\"", value)
}

def paymentProvider() {
    def value = project.getProperties().get("paymentProvider")
    if (value == null || !value?.trim()) {
        return "\"\""
    }
    return String.format("\"%s\"", value)
}

def proServerUrl() {
    def value = project.getProperties().get("proServerUrl")
    if (value == null || !value?.trim()) {
        return "\"\""
    }
    return String.format("\"%s\"", value)
}

repositories {
    flatDir {
        dirs 'libs'
    }
    jcenter()
    google()
    mavenCentral()
    mavenLocal()
    maven { url  "https://dl.bintray.com/piasy/maven" }
    maven { url "https://www.jitpack.io" }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.0'
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable-jvm:0.3.3"
    implementation 'com.google.protobuf:protobuf-javalite:3.10.0'

    implementation fileTree(dir: "libs", include:"liblantern-${androidArch()}.aar")

    implementation(name:'paymentwall-android-sdk', ext: 'aar')

    implementation 'com.adyen.checkout:core:1.14.1'
    implementation 'com.adyen.checkout:utils:1.14.1'
    implementation 'com.adyen.checkout:ui:1.14.1'
    implementation 'com.adyen.checkout:cardscan:1.14.1'

    implementation project(':alipaySdk-15.6.5-20190718211148')
    implementation(name:'alipayadapter-release', ext: 'aar') {
        transitive = true
    }

    // https://mvnrepository.com/artifact/net.jodah/expiringmap
    implementation group: 'net.jodah', name: 'expiringmap', version: '0.5.9'

    implementation 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:1.6.1@aar'

    implementation fileTree(dir: 'libs', include: '*.jar')

    implementation 'com.squareup.okhttp3:okhttp:4.9.1'

    // The below aar is built from https://github.com/getlantern/FinestWebView-Android
    implementation(name:'finestwebview', ext: 'aar')
    implementation 'com.kyleduo.switchbutton:library:1.4.6'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'


    // https://mvnrepository.com/artifact/joda-time/joda-time
    implementation group: 'joda-time', name: 'joda-time', version: '2.8.2'


    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.3'
    profileImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.android.billingclient:billing:3.0.1'
    implementation "androidx.constraintlayout:constraintlayout:2.1.0-beta01"
    implementation "androidx.recyclerview:recyclerview:1.2.0-rc01"

    // lib that simplifies event bus communication between activities, fragments, threads, services, etc
    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation group: 'commons-codec', name: 'commons-codec', version: '1.12'

    // https://mvnrepository.com/artifact/javax.mail/mail
    implementation group: 'javax.mail', name: 'mail', version: '1.4.7'

    // FinestWebView dependencies
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'com.thefinestartist:utils:0.9.0'
    implementation (group: 'com.google.guava', name: 'guava', version: '23.0-android') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    implementation 'com.stripe:stripe-android:10.3.1'

    implementation 'com.github.bumptech.glide:glide:4.9.0'

    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:26.1.1')

    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-config'

    annotationProcessor "org.androidannotations:androidannotations:$androidAnnotationsVersion"
    kapt 'com.github.bumptech.glide:compiler:4.7.1'
    implementation("org.androidannotations:androidannotations-api:$androidAnnotationsVersion")
    kapt "org.androidannotations:androidannotations:$androidAnnotationsVersion"

    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.1'

    testImplementation 'junit:junit:4.13.1'

    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.github.YarikSOffice:lingver:1.3.0"
    implementation 'com.github.getlantern:secrets-android:3ac588e'
    implementation 'com.github.getlantern:db-android:806f292832'
}

apply plugin: 'com.google.gms.google-services'
