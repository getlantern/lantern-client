plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dev.flutter.flutter-gradle-plugin'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.protobuf'
    id "io.sentry.android.gradle" version "4.2.0"
}

android {
    ndkVersion "26.0.10792818"

    bundle {
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
        language {
            // Specifies that the app bundle should not support configuration APKs for language resources.
            // These resources are instead packaged with each base and dynamic feature APK.
            // See https://stackoverflow.com/a/52733674
            enableSplit = false
        }
    }

    flavorDimensions "versionCode"

    useLibrary 'org.apache.http.legacy'

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/maven/commons-io/commons-io/pom.xml'
        exclude 'META-INF/maven/commons-io/commons-io/pom.properties'
        exclude("META-INF/*.kotlin_module")
        exclude 'README.txt'

        jniLibs {
            useLegacyPackaging true
        }
    }


//    buildFeatures {
//        viewBinding true
//        dataBinding true
//    }

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    // Use now as the user-visible version
    def now = new Date()

    // Use configured versionCode if available

    def code = getInt("versionCode")

    if (code == 0) {
        // Calculate the versionCode as the number of seconds from a fixed start
        // point so that it's always increasing.
        def start = new Date(2015, 1, 1).getTime()
        code = (int) ((now.getTime() - start) / 1000)
    }

    defaultConfig {

        multiDexEnabled = true

        // Include this CMakeLists.txt file in the build pipeline
        externalNativeBuild {
            cmake {
                cppFlags ''
                arguments '-DANDROID_STL=c++_shared'
            }
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['resourcePackageName': 'org.getlantern.lantern', "androidManifestFile": "$projectDir/src/main/AndroidManifest.xml".toString()]
            }
        }

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        applicationId "org.getlantern.lantern"

        ndk {
            // Specifies the ABI configurations of native libraries Gradle should build and package with the app.
            abiFilters = ["x86_64", "arm64-v8a"]
        }

        versionCode code

        if (project.hasProperty('lanternVersion')) {
            versionName "$project.lanternVersion ($project.lanternRevisionDate)"
        } else {
            versionName "9999.99.99"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    externalNativeBuild {
        cmake {
            path file('cmake/CMakeLists.txt')
            version '3.22.1'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled = true
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        release {
            if (project.hasProperty("KEYSTORE_FILE")) {
                storeFile file(KEYSTORE_FILE)
                keyAlias "Lantern Release Key"
                storePassword KEYSTORE_PWD
                keyPassword KEY_PWD
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            buildConfigField "boolean", "DEVELOPMENT_MODE", "true"
            buildConfigField "boolean", "CI", getBoolean("ci")
            buildConfigField "boolean", "STICKY_CONFIG", getBoolean("stickyConfig")
            buildConfigField "boolean", "STAGING", getBoolean("useStaging")
            buildConfigField "boolean", "PLAY_VERSION", getBoolean("playVersion")
            buildConfigField "String", "COUNTRY", userCountry()
            buildConfigField "String", "PAYMENT_PROVIDER", paymentProvider()
            buildConfigField "String", "PRO_SERVER_URL", proServerUrl()
            buildConfigField "String", "SIGNING_CERTIFICATE_SHA256", "\"\""
            buildConfigField "String", "LOCONF_URL", '"https://raw.githubusercontent.com/getlantern/loconf/master/test-messages.json"'
            buildConfigField "String", "PRIVACY_POLICY_URL", '"https://s3.amazonaws.com/lantern/LanternPrivacyPolicy.pdf"'
            buildConfigField "String", "TERMS_OF_SERVICE_URL", '"https://s3.amazonaws.com/lantern/Lantern-TOS.pdf"'
        }

        profile {
            minifyEnabled false
            shrinkResources false
            debuggable false
            buildConfigField "boolean", "DEVELOPMENT_MODE", "true"
            buildConfigField "boolean", "CI", getBoolean("ci")
            buildConfigField "boolean", "STICKY_CONFIG", getBoolean("stickyConfig")
            buildConfigField "boolean", "STAGING", getBoolean("useStaging")
            buildConfigField "boolean", "PLAY_VERSION", getBoolean("playVersion")
            buildConfigField "String", "COUNTRY", userCountry()
            buildConfigField "String", "SIGNING_CERTIFICATE_SHA256", "\"\""
            buildConfigField "String", "PAYMENT_PROVIDER", paymentProvider()
            buildConfigField "String", "PRO_SERVER_URL", proServerUrl()
            buildConfigField "String", "LOCONF_URL", '"https://raw.githubusercontent.com/getlantern/loconf/master/test-messages.json"'
            buildConfigField "String", "PRIVACY_POLICY_URL", '"https://s3.amazonaws.com/lantern/LanternPrivacyPolicy.pdf"'
            buildConfigField "String", "TERMS_OF_SERVICE_URL", '"https://s3.amazonaws.com/lantern/Lantern-TOS.pdf"'
        }

        release {
            minifyEnabled true
            shrinkResources true
            debuggable false

            buildConfigField "boolean", "DEVELOPMENT_MODE", getBoolean("developmentMode")
            buildConfigField "boolean", "CI", "false"
            buildConfigField "boolean", "STICKY_CONFIG", getBoolean("stickyConfig")
            buildConfigField "boolean", "STAGING", getBoolean("useStaging")
            buildConfigField "boolean", "PLAY_VERSION", getBoolean("playVersion")
            buildConfigField "String", "COUNTRY", userCountry()
            buildConfigField "String", "SIGNING_CERTIFICATE_SHA256", "\"108f612ae55354078ec12b10bb705362840d48fa78b9262c11b6d0adeff6f289\""
            buildConfigField "String", "PAYMENT_PROVIDER", paymentProvider()
            buildConfigField "String", "PRO_SERVER_URL", proServerUrl()
            buildConfigField "String", "LOCONF_URL", '"https://raw.githubusercontent.com/getlantern/loconf/master/messages.json"'
            buildConfigField "String", "PRIVACY_POLICY_URL", '"https://s3.amazonaws.com/lantern/LanternPrivacyPolicy.pdf"'
            buildConfigField "String", "TERMS_OF_SERVICE_URL", '"https://s3.amazonaws.com/lantern/Lantern-TOS.pdf"'
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        play {
            initWith release
            buildConfigField "boolean", "PLAY_VERSION", "true"
            ndk.abiFilters = abiFilters()
        }

        sideload {
            initWith release
            buildConfigField "boolean", "PLAY_VERSION", "false"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            java.srcDirs += 'src/main/kotlin'
            proto.srcDirs += '../../protos_shared'
        }
    }

    productFlavors {
        prod {
        }
        autoTest {
            versionCode 1 // so we can always test auto-update.
        }
        appiumTest {
            buildConfigField "boolean", "CI", "true"
        }
    }

    splits {
        abi {
            enable !bool("playVersion")
            reset()
            include androidArchJava()
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'org.getlantern.lantern'
}

def androidArch() {
    def value = project.getProperties().get("androidArch")
    if (value == null || !value?.trim()) {
        return "all"
    }
    return value
}

def androidArchJava() {
    def value = project.getProperties().get("androidArchJava")
    if (value == null || !value?.trim()) {
        return "armeabi-v7a arm64-v8a x86_64"
    }
    return value
}

def getBoolean(name) {
    def value = project.getProperties().get(name)
    if (value == null || !value?.trim()) {
        return "false"
    }
    return value
}

def bool(name) {
    def value = project.getProperties().get(name)
    return "true".equalsIgnoreCase(value)
}

def abiFilters() {
    if (bool("playVersion")) {
        // Flutter does not support 32 bit x86. Some of our other native libraries do
        // support x86, which by default means that we'll include an x86 folder of native
        // libs. When this is present, 32 bit x86 devices will attempt to run the app as an
        // x86 app and will crash because there's no x86 flutter.so. As a workaround for
        // this, we omit the x86 architecture entirely so that such clients will run Lantern
        // in ARM emulated mode.
        //
        // SO, DO NOT INCLUDE x86 HERE!!!
        return ['arm64-v8a', 'armeabi-v7a', 'x86_64']
    }
    return []
}

def getInt(name) {
    def value = project.getProperties().get(name)
    if (value == null || !value?.trim()) {
        return 0
    }
    return value.toInteger()
}

def userCountry() {
    def value = project.getProperties().get("country")
    if (value == null || !value?.trim()) {
        return "\"\""
    }
    return String.format("\"%s\"", value)
}

def paymentProvider() {
    def value = project.getProperties().get("paymentProvider")
    if (value == null || !value?.trim()) {
        return "\"\""
    }
    return String.format("\"%s\"", value)
}

def proServerUrl() {
    def value = project.getProperties().get("proServerUrl")
    if (value == null || !value?.trim()) {
        return "\"\""
    }
    return String.format("\"%s\"", value)
}

repositories {
    flatDir {
        dirs 'libs'
    }
    google()
    mavenCentral()
    mavenLocal()
    maven { url "https://www.jitpack.io" }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protoc_version"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

flutter {
    source '../..'
}

configurations {
    debugImplementation.exclude group: "junit", module: "junit"
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable-jvm:0.3.5"
    implementation "com.google.protobuf:protobuf-javalite:$protoc_version"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugarJdk"
    implementation fileTree(dir: "libs", include: "liblantern-${androidArch()}.aar")
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    implementation 'androidx.webkit:webkit:1.8.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    // https://mvnrepository.com/artifact/joda-time/joda-time
    implementation group: 'joda-time', name: 'joda-time', version: '2.8.2'

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'

    // Google Play libraries
    implementation 'com.android.billingclient:billing:6.2.0'

    // lib that simplifies event bus communication between activities, fragments, threads, services, etc
    implementation 'org.greenrobot:eventbus:3.3.1'

    // https://mvnrepository.com/artifact/net.jodah/expiringmap
//    implementation group: 'net.jodah', name: 'expiringmap', version: '0.5.9'
//    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
//    implementation group: 'commons-codec', name: 'commons-codec', version: '1.12'
//     https://mvnrepository.com/artifact/javax.mail/mail
//    implementation group: 'javax.mail', name: 'mail', version: '1.4.7'

    implementation 'com.stripe:stripe-android:20.17.0'
    annotationProcessor "org.androidannotations:androidannotations:$androidAnnotationsVersion"
    implementation("org.androidannotations:androidannotations-api:$androidAnnotationsVersion")
    kapt "org.androidannotations:androidannotations:$androidAnnotationsVersion"

    implementation "com.github.YarikSOffice:lingver:1.3.0"
    // from https://github.com/getlantern/opus_android
    implementation files('libs/opuslib-release.aar')
    implementation 'com.github.getlantern:secrets-android:f6a7a69f3d'
    implementation 'com.github.getlantern:db-android:5a082e0bdd'
    implementation 'com.github.getlantern:messaging-android:ca369d5173'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'
    //Test implementation
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.annotation:annotation:1.6.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.squareup.okhttp3:okhttp:4.9.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.13.5"
}

apply plugin: 'com.google.gms.google-services'

sentry {

    // Disables or enables debug log output, e.g. for for sentry-cli.
    // Default is disabled.
    debug = false

    // The slug of the Sentry organization to use for uploading proguard mappings/source contexts.
    org = "getlantern"

    // The slug of the Sentry project to use for uploading proguard mappings/source contexts.
    projectName = "android"

    // The authentication token to use for uploading proguard mappings/source contexts.
    // WARNING: Do not expose this token in your build.gradle files, but rather set an environment
    // variable and read it into this property.
    authToken = System.getenv("SENTRY_AUTH_TOKEN")

    // The url of your Sentry instance. If you're using SAAS (not self hosting) you do not have to
    // set this. If you are self hosting you can set your URL here
    url = null

    // Disables or enables the handling of Proguard mapping for Sentry.
    // If enabled the plugin will generate a UUID and will take care of
    // uploading the mapping to Sentry. If disabled, all the logic
    // related to proguard mapping will be excluded.
    // Default is enabled.
    includeProguardMapping = true


    // Whether the plugin should attempt to auto-upload the mapping file to Sentry or not.
    // If disabled the plugin will run a dry-run and just generate a UUID.
    // The mapping file has to be uploaded manually via sentry-cli in this case.
    // Default is enabled.
    autoUploadProguardMapping = true

    // Disables or enables the automatic configuration of Native Symbols
    // for Sentry. This executes sentry-cli automatically so
    // you don't need to do it manually.
    // Default is disabled.
    uploadNativeSymbols = true

    // Does or doesn't include the source code of native code for Sentry.
    // This executes sentry-cli with the --include-sources param. automatically so
    // you don't need to do it manually.
    // Default is disabled.
    includeNativeSources = false
}
